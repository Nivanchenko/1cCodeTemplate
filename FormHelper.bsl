
Функция ПараметрыСозданияЭлементов() Экспорт 
	
	Результат = Новый Структура;
	
	Результат.Вставить("Имя");
	Результат.Вставить("Тип");
	Результат.Вставить("Родитель");
	Результат.Вставить("Место");
	Результат.Вставить("ПутьКДанным");
	Результат.Вставить("Вид");
	Результат.Вставить("Обработчики");
	
	Возврат Результат;
	
КонецФункции

Процедура СоздатьРеквизитФормы(Форма, Имя, Тип) Экспорт 
	
	ДобавляемыеРеквизиты = Новый Массив; 
	НовыйРеквизит = Новый РеквизитФормы(Имя, Тип); 
	ДобавляемыеРеквизиты.Добавить(НовыйРеквизит); 	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты); 
	
КонецПроцедуры

//Тип - строка, например СправочникСсылка.Контрагенты
Процедура СоздатьРевизитСсылка(Форма, Имя, ТипСсылки) Экспорт 
	
	ТипРеквизита = Новый ОписаниеТипов(ТипСсылки);
	
	СоздатьРеквизитФормы(Форма, Имя, ТипРеквизита); 
	
КонецПроцедуры
 
Процедура СоздатьРевизитДата(Форма, Имя, ЧастьДаты) Экспорт 
	
	ТипРеквизита = Новый ОписаниеТипов("Дата",,,Новый КвалификаторыДаты(ЧастьДаты));
	
	СоздатьРеквизитФормы(Форма, Имя, ТипРеквизита); 
	
КонецПроцедуры
	
Процедура СоздатьРевизитБулево(Форма, Имя) Экспорт 
	
	ТипРеквизита = Новый ОписаниеТипов("Булево");
	
	СоздатьРеквизитФормы(Форма, Имя, ТипРеквизита); 
	
КонецПроцедуры
	
Процедура СоздатьРевизитЧисло(Форма, Имя, Размер, Точность) Экспорт 
	
	ТипРеквизита = Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(Размер, Точность));
	
	СоздатьРеквизитФормы(Форма, Имя, ТипРеквизита); 
	
КонецПроцедуры
	
Процедура СоздатьРевизитСтрока(Форма, Имя, Длина = Неопределено) Экспорт 
	
	Если Длина = Неопределено Тогда
		ТипРеквизита = Новый ОписаниеТипов("Строка");
	Иначе
		ТипРеквизита = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(Длина));
	КонецЕсли;
	СоздатьРеквизитФормы(Форма, Имя, ТипРеквизита); 
	
КонецПроцедуры
	
Функция СоздатьЭлементФормы(Форма, ПараметрыСоздания) Экспорт 
	
	НовыйЭлемент = Форма.Элементы.Добавить(ПараметрыСоздания.Имя, ПараметрыСоздания.Тип, ПараметрыСоздания.Родитель);
	НовыйЭлемент.Вид = ПараметрыСоздания.Вид;
	НовыйЭлемент.ПутьКДанным = ПараметрыСоздания.ПутьКДанным;
	
	Если не ПараметрыСоздания.Место = Неопределено Тогда
		Форма.Элементы.Переместить(НовыйЭлемент, ПараметрыСоздания.Родитель, ПараметрыСоздания.Место); 
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыСоздания.Обработчики) = Тип("Структура") Тогда
		Для Каждого Обработчик из ПараметрыСоздания.Обработчики цикл
			НовыйЭлемент.УстановитьДействие(Обработчик.Ключ, Обработчик.Значение);	
		КонецЦикла;
	КонецЕсли;
	
	Возврат НовыйЭлемент;

КонецФункции

Функция СоздатьПолеВвода(Форма, Имя, ПутьКДанным, Родитель, Место = Неопределено, Обработчики = Неопределено) Экспорт 
	
	Параметры = ПараметрыСозданияЭлементов();
	
	Параметры.Имя = Имя;
	Параметры.Тип = Тип("ПолеФормы");
	Параметры.Родитель = Родитель;
	Параметры.Место = Место;
	Параметры.ПутьКДанным = ПутьКДанным;
	Параметры.Вид = ВидПоляФормы.ПолеВвода;
	Параметры.Обработчики = Обработчики;
	
	Возврат СоздатьЭлементФормы(Форма, Параметры);
	
КонецФункции

Функция СоздатьПолеФлажка(Форма, Имя, ПутьКДанным, Родитель, Место = Неопределено, Обработчики = Неопределено) Экспорт 
	
	Параметры = ПараметрыСозданияЭлементов();
	
	Параметры.Имя = Имя;
	Параметры.Тип = Тип("ПолеФормы");
	Параметры.Родитель = Родитель;
	Параметры.Место = Место;
	Параметры.ПутьКДанным = ПутьКДанным;
	Параметры.Вид = ВидПоляФормы.ПолеФлажка;
	Параметры.Обработчики = Обработчики;
	
	Возврат СоздатьЭлементФормы(Форма, Параметры);
	
КонецФункции

Функция СоздатьКоманду(Форма, ИмяКоманды, Заголовок, ИмяОбработчика) Экспорт 
	НоваяКоманда = Форма.Команды.Добавить(ИмяКоманды);
	НоваяКоманда.Заголовок = Заголовок;
	НоваяКоманда.Действие = ИмяОбработчика; 
	
	Возврат НоваяКоманда;
КонецФункции

Функция СоздатьЭлементКнопка(Форма, Имя, Родитель, ИмяКоманды, Заголовок, Место = Неопределено) Экспорт 
	
	НовыйЭлемент = Форма.Элементы.Добавить(Имя, Тип("КнопкаФормы"),Родитель);
	НовыйЭлемент.Заголовок = Заголовок;
	НовыйЭлемент.ИмяКоманды = ИмяКоманды;
	
	Если не Место = Неопределено Тогда
		Форма.Элементы.Переместить(НовыйЭлемент, Родитель, Место); 
	КонецЕсли;
	
	Возврат НовыйЭлемент;
	
КонецФункции

Функция СоздатьКнопку(Форма, Имя, Родитель, ИмяКоманды, Заголовок, ИмяОбработчика, Место = Неопределено) Экспорт 
	
	Команда = СоздатьКоманду(Форма, ИмяКоманды, Заголовок, ИмяОбработчика);
	ЭлементКнопки = СоздатьЭлементКнопка(Форма, Имя, Родитель, ИмяКоманды, Заголовок, Место);
	
	Возврат ЭлементКнопки;
КонецФункции
